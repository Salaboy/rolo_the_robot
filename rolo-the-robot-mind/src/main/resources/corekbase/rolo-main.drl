package com.salaboy.rolo;

import com.salaboy.rolo.model.*;
import com.salaboy.rolo.model.reports.*;
import com.salaboy.rolo.*;

import com.salaboy.rolo.body.api.*;

import com.salaboy.rolo.events.*;

import com.salaboy.rolo.the.robot.comm.*;

import org.kie.internal.event.rule.*;
import org.kie.api.runtime.rule.*;
import org.drools.core.common.*;

import javax.enterprise.event.*;

declare DistanceReport
    @role(event)
    @expires(1s)
end

global Event notifications;

global Event external;

rule "robot part logger"
    when
       $rP: RobotPart()
    then
        System.out.println(">>> A new robot part was added: "+ $rP);
        external.fire(new ExternalNotificationEvent(">>> A new robot part was added: "+ $rP));
end

rule "looking for something to do"
    salience 10
    when
        $fW: RobotFrontWheels()
    then
        $fW.forward();
        notifications.fire(new MindNotificationEvent(" Looking for something to do!"));
end


rule "poll front sonar every 3s"
    timer ( int: 0s 3s )
    when
        $dS: RobotSonar(name == "sonar-front")
    then
        $dS.readDistance();
end

rule "poll right sonar every 3s"
    timer ( int: 0s 4s )
    when
        $dS: RobotSonar(name == "sonar-right")
    then
        $dS.readDistance();
end

rule "poll left sonar every 3s"
    timer ( int: 0s 4s )
    when
        $dS: RobotSonar(name == "sonar-left")
    then
        $dS.readDistance();
end


rule "If there is something closer than 30 cm turn left 90 degrees"
    
    when
        $n: Number( doubleValue < 30 && doubleValue > 0 ) from accumulate (DistanceReport(sensorName == "SONAR_FRONT", $distance: distance) over window:time( 1s ) from entry-point "distance-sensor", average($distance))
        $fW: RobotFrontWheels()
    then
        $fW.rotateAndMove("left", 90, "forward");
        notifications.fire(new MindNotificationEvent(" Rotating 90 degrees left because there is something in front of me ->" + $n));
        
end

rule "If there is something on the left closer than 15 cm turn 10 degrees right"
    when
        $n: Number( doubleValue < 15 && doubleValue > 0 ) from accumulate (DistanceReport(sensorName == "SONAR_LEFT", $distance: distance) over window:time( 1s ) from entry-point "distance-sensor", average($distance))
        $fW: RobotFrontWheels()
    then
        $fW.rotateAndMove("right", 10, "forward");
        notifications.fire(new MindNotificationEvent(" Rotating 10 degrees right to move away from the object on the left ->" + $n));   
end

rule "If there is something on the right closer than 15 cm turn 10 degrees left"
    when
        $n: Number( doubleValue < 15 && doubleValue > 0 ) from accumulate (DistanceReport(sensorName == "SONAR_RIGHT", $distance: distance) over window:time( 1s ) from entry-point "distance-sensor", average($distance))
        $fW: RobotFrontWheels()
    then
        $fW.rotateAndMove("left", 10, "forward");
        notifications.fire(new MindNotificationEvent(" Rotating 10 degrees left to move away from the object on the right ->" + $n));   
end




/*rule "Something is getting Closer" 
    when
        $r: RoloTheRobot()
        RobotSonar( $sensor: name )
        $n: Number( doubleValue < 0.5 ) from accumulate (DistanceReport( sensorName == $sensor, $d: distance ) over window:time( 1000ms ) from entry-point "distance-sensor", tendency($d))
        
    then
       System.out.println("WARN: Something is getting closer" + $n);
end

rule "Something is going away" 
    when
        $r: RoloTheRobot()
        RobotSonar( $sensor: name )
        $n: Number( doubleValue > 0.5 ) from accumulate (DistanceReport( sensorName == $sensor, $d: distance ) over window:time( 1000ms ) from entry-point "distance-sensor", tendency($d))
        
    then
       System.out.println("WARN: Something is going away" + $n);
end*/